cmake_minimum_required (VERSION 2.8.11)

project (qaul)

# define paths
set(LIBQAUL_SRC @CMAKE_SOURCE_DIR@/src/libqaul)
set(LIBQAUL_BUILD @CMAKE_BINARY_DIR@/src/libqaul)
set(QAULWIN_SRC @CMAKE_SOURCE_DIR@/src/client/win/src)
set(QAUL_SRC @CMAKE_SOURCE_DIR@)
set(QAUL_BUILD @CMAKE_BINARY_DIR@)

# set path to mingw install location
get_filename_component(MINGW_PATH @CMAKE_CXX_COMPILER@ PATH)

# set /clr flag for compiler
STRING(REPLACE "/EHsc" "/EHa" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
STRING(REPLACE "/RTC1" "" CMAKE_CXX_FLAGS_DEBUG ${CMAKE_CXX_FLAGS_DEBUG})
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /clr")

# set properties and flags
add_definitions(-DUNICODE -D_UNICODE)
set_property(GLOBAL PROPERTY USE_FOLDERS ON)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4")
message("cxx Flags: " ${CMAKE_CXX_FLAGS})

# path to liblibqaul.dll.a directory
link_directories(${LIBQAUL_BUILD})

# add source files
file(GLOB QAULTARGET_FILES
	"${QAULWIN_SRC}/*.cpp"
	"${QAULWIN_SRC}/*.h"
	"${QAULWIN_SRC}/*.resx"
	"${QAULWIN_SRC}/*.rc"
	"${QAULWIN_SRC}/*.ico"
)

# add target
add_executable(qaul 
	${QAULTARGET_FILES}
)
set_property(TARGET qaul PROPERTY VS_GLOBAL_KEYWORD "ManagedCProj")
set_property(TARGET qaul PROPERTY VS_DOTNET_REFERENCES 
	"System"
	"System.Windows.Forms"
	"System.Data"
	"System.Drawing"
)
set_property(TARGET qaul PROPERTY LINK_FLAGS 
	"/FORCE:MULTIPLE /level='requireAdministrator' /uiAccess='false'"
)
# Hide console window on startup when built for RELEASE
set_property(TARGET qaul PROPERTY LINK_FLAGS_RELEASE
	"/SUBSYSTEM:WINDOWS /ENTRY:main"
)
target_include_directories(qaul PRIVATE
	${LIBQAUL_SRC}/include
)
target_link_libraries(qaul
)

# copy libraries and files into build directory
add_custom_command(TARGET qaul POST_BUILD
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${LIBQAUL_BUILD}/libqaul.dll"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_directory
		"${QAUL_SRC}/GUI/www"
		"$<TARGET_FILE_DIR:qaul>/www"
	COMMAND ${CMAKE_COMMAND} -E copy_directory
		"${QAUL_SRC}/GUI/files"
		"$<TARGET_FILE_DIR:qaul>/files"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${QAUL_BUILD}/third_party/olsr/src/olsr/olsrd.exe"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${QAUL_BUILD}/third_party/olsr/src/olsr/lib/olsrd_qaul/olsrd_qaul.dll"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${QAUL_SRC}/distfiles/win/olsrd_win.conf"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${MINGW_PATH}/libgcc_s_dw2-1.dll"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${MINGW_PATH}/libwinpthread-1.dll"
		"$<TARGET_FILE_DIR:qaul>"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${QAULWIN_SRC}/app.ico"
		"$<TARGET_FILE_DIR:qaul>"
)


install(TARGETS 
	qaul 
	DESTINATION bin
)
install(DIRECTORY
	"${QAUL_SRC}/GUI/www"
	"${QAUL_SRC}/GUI/files"
	DESTINATION bin
)
install(FILES 
	"${LIBQAUL_BUILD}/libqaul.dll"
	"${QAUL_BUILD}/third_party/olsr/src/olsr/olsrd.exe"
	"${QAUL_BUILD}/third_party/olsr/src/olsr/lib/olsrd_qaul/olsrd_qaul.dll"
	"${QAUL_SRC}/distfiles/win/olsrd_win.conf"
	"${MINGW_PATH}/libgcc_s_dw2-1.dll"
	"${MINGW_PATH}/libwinpthread-1.dll"
	"${QAULWIN_SRC}/app.ico"
	DESTINATION bin
)

set(CPACK_GENERATOR NSIS)
set(CPACK_PACKAGE_NAME "qaul.net")
set(CPACK_PACKAGE_VENDOR "qaul.net community")
set(CPACK_PACKAGE_CONTACT "contact@qaul.net")
set(CPACK_PACKAGE_VERSION @CPACK_PACKAGE_VERSION@)
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Decentralized, Internet independent Wifi-mesh communication app")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "qaul.net")
set(CPACK_PACKAGE_EXECUTABLES "qaul" "qaul.net")
set(CPACK_PACKAGE_ICON "${QAUL_SRC}/distfiles/win\\\\installer-icon.bmp")
set(CPACK_NSIS_MUI_ICON "${QAUL_SRC}/distfiles/win/installer.ico")
set(CPACK_NSIS_MUI_UNIICON "${QAUL_SRC}/distfiles/win/uninstaller.ico")
set(CPACK_NSIS_HELP_LINK "http://qaul.net")
set(CPACK_NSIS_MODIFY_PATH ON)
set(CPACK_RESOURCE_FILE_LICENSE "${QAUL_SRC}/Licenses/qaul.net_license.txt")

include(CPack)
